#INCLUDE P16F877A.INC
__CONFIG _HS_OSC &_WDT_OFF &_LVP_OFF
CBLOCK 0X20
	LED1, LED2, LED3, LED4, LED5,
	NGUONLED1, NGUONLED2, NGUONLED3, NGUONLED4, NGUONLED5,
	QUET_LED, NHAP_NHAY, NHAP_NHAY_TEMP,
	MENU, SO, SO_TEMP, VONG1, VONG2, VONG3,
	W_TEMP, STATUS_TEMP
ENDC
ORG 0X00
	GOTO MAIN
ORG 0X04
	GOTO BEGIN_NGAT
ORG 0X05
MAIN
	;Cài đặt Vào/Ra
	BSF STATUS,RP0	 ;Chọn Banksel 1
	CLRF TRISA
	BSF TRISB,0
	BSF TRISB,1
	BSF TRISB,2
	BCF TRISB,4
	BCF TRISB,5
	CLRF TRISD

	;Cài đặt Timer0 1:8
	MOVLW B'00000010'
	MOVWF OPTION_REG

	;Cài đặt ngắt Timer0
	BCF STATUS, RP0	 ;Chọn Banksel 0
	MOVLW D'131'
	MOVWF TMR0 	;Cài đặt Timer0 tràn 1ms
	BSF INTCON,7	 ;Cho phép ngắt toàn cục
	BSF INTCON,5 	;Cho phép ngắt Timer0

	;Xóa cờ ngắt
	BCF INTCON,2	 ;Xóa cờ ngắt Timer0
	BCF STATUS, RP0	 ;Chọn Banksel 0
	;Tắt tất cả led 7 đoạn
	MOVLW 0XFF
	MOVWF PORTA
	MOVWF PORTD

	;Cài đặt giá trị ban đầu cho led 7 đoạn
	MOVLW D'0'
	MOVWF LED1
	MOVWF LED2
	MOVWF LED3
	MOVWF LED4
	MOVWF LED5

	;Cài đặt chân Anot cho led 7 đoạn(Chân nguồn)
	MOVLW 0XFE
	MOVWF NGUONLED1
	MOVLW 0XFD
	MOVWF NGUONLED2
	MOVLW 0XFB
	MOVWF NGUONLED3
	MOVLW 0XF7
	MOVWF NGUONLED4
	MOVLW 0XDF
	MOVWF NGUONLED5

	;Cài đặt giá trị QUET_LED
	BCF STATUS,C 	;XOA BIT CO C
	MOVLW B'00000001'
	MOVWF QUET_LED
	MOVLW B'00100000'
	MOVWF MENU
	MOVLW D'255'
	MOVWF NHAP_NHAY
	MOVLW 0XFF
	MOVWF NHAP_NHAY_TEMP
	;Kiểm tra phím bấm
KTMENU_0		;Kiểm tra nhấn Menu
	BCF PORTB,4
	BTFSC PORTB,1
	GOTO KTUP_0
	GOTO KTMENU_0_2
KTMENU_0_2	;Chống nhiễu cho phím Menu
	BSF PORTB,4
	CALL DELAY
	BTFSC PORTB,1
	GOTO KTUP_0
	GOTO KTMENU_1
KTMENU_1		;Xử lý khi nhả nút nhấn Menu
	BTFSS PORTB,1
	GOTO KTMENU_1
	CALL NHAN_MENU
	GOTO KTUP_0
KTUP_0	;Kiểm tra nhấn Up
	BCF PORTB,4
	BTFSC PORTB,0
	GOTO KTDOWN_0
	GOTO KTUP_0_2
KTUP_0_2		;Chống nhiễu cho nút Up
	BSF PORTB,4
	CALL DELAY
	BTFSC PORTB,0
	GOTO KTDOWN_0
	GOTO KTUP_1
KTUP_1		;Xử lý khi nhả nút Up
	BTFSS PORTB,0
	GOTO KTUP_1
	CALL UP_SO
	GOTO KTDOWN_0
KTDOWN_0		;Kiểm tra nhấn Down
	BCF PORTB,4
	BTFSC PORTB,2
	GOTO KTMENU_0
	GOTO KTDOWN_0_2
KTDOWN_0_2		;Chống nhiễu nút Down
	BSF PORTB,4
	CALL DELAY
	BTFSC PORTB,2
	GOTO KTMENU_0
	GOTO KTDOWN_1
KTDOWN_1			;Xử lý khi nhả Down
	BTFSS PORTB,2
	GOTO KTDOWN_1
	CALL DOWN_SO
	GOTO KTMENU_0
BANGMA
	ADDWF PCL,F
	RETLW 0XC0
	RETLW 0XF9
	RETLW 0XA4
	RETLW 0XB0
	RETLW 0X99
	RETLW 0X92
	RETLW 0X82
	RETLW 0XF8
	RETLW 0X80
	RETLW 0X90
BEGIN_NGAT 		;Chương trình ngắt
	MOVWF W_TEMP
	SWAPF STATUS,W
	MOVWF STATUS_TEMP
	CLRF STATUS
	;Bắt đầu chương trình con phục vụ ngắt
	;Nhấp nháy led 7 đoạn được chọn
	DECFSZ NHAP_NHAY,F
	GOTO $+8
	BTFSS NHAP_NHAY_TEMP,0	;Kiểm tra trạng thái bit 0 của biến trung gian
	GOTO $+3
	BCF NHAP_NHAY_TEMP,0
	GOTO $+2
	BSF NHAP_NHAY_TEMP,0
	MOVLW D'255'		;Cài đặt lại giá trị ban đầu cho biến NHAP_NHAY
	MOVWF NHAP_NHAY
	;Kết thúc đoạn mã tạo Nhấp nháy led 7 đoạn
	
	;Nhấp nháy led đơn khi vào chế độ sửa chửa
	BTFSC MENU,5
	GOTO $+5
	BTFSS NHAP_NHAY_TEMP,0
	GOTO $+3
	BCF PORTB,5
	GOTO $+2
	BSF PORTB,5
	CLRF STATUS
LED_1	
	BTFSS QUET_LED,0       ;Kiểm tra quét led 1 (7 đoạn)
	GOTO LED_2
	BTFSS MENU,0
	GOTO $+3
	BTFSS NHAP_NHAY_TEMP,0
	GOTO OFF_LED
	MOVFW LED1
	CALL BANGMA
	MOVWF PORTD
	MOVFW NGUONLED1
	MOVWF PORTA
	RLF QUET_LED,1       ;Dịch trái QUET_LED
	GOTO END_NGAT
LED_2
	BTFSS QUET_LED,1       ;Kiểm tra quét led 2
	GOTO LED_3
	BTFSS MENU,1
	GOTO $+3
	BTFSS NHAP_NHAY_TEMP,0
	GOTO OFF_LED
	MOVFW LED2
	CALL BANGMA
	MOVWF PORTD
	MOVFW NGUONLED2
	MOVWF PORTA
	BCF PORTD,7	   ; Hiển thị dấu phẩy ngăn cách hang nghìn
	RLF QUET_LED,1       ; Dịch trái QUET_LED
	GOTO END_NGAT
LED_3
	BTFSS QUET_LED,2       ;Kiểm tra quét led 3
	GOTO LED_4
	BTFSS MENU,2
	GOTO $+3
	BTFSS NHAP_NHAY_TEMP,0
	GOTO OFF_LED
	MOVFW LED3
	CALL BANGMA
	MOVWF PORTD
	MOVFW NGUONLED3
	MOVWF PORTA
	RLF QUET_LED,1       ; Dịch trái QUET_LED
	GOTO END_NGAT
LED_4
	BTFSS QUET_LED,3       ;Kiểm tra quét led 4
	GOTO LED_5
	BTFSS MENU,3
	GOTO $+3
	BTFSS NHAP_NHAY_TEMP,0
	GOTO OFF_LED
	MOVFW LED4
	CALL BANGMA
	MOVWF PORTD
	MOVFW NGUONLED4
	MOVWF PORTA
	RLF QUET_LED,1       ; Dịch trái QUET_LED
	GOTO END_NGAT
LED_5
	BTFSS MENU,4       ;Quét led 5
	GOTO $+3
	BTFSS NHAP_NHAY_TEMP,0
	GOTO $+4
	MOVFW NGUONLED5
	MOVWF PORTA
	GOTO $ + 3
	MOVLW 0XFF
	MOVWF PORTA
	MOVFW LED5
	CALL BANGMA
	MOVWF PORTD
	MOVLW B'00000001'
	MOVWF QUET_LED
	GOTO END_NGAT
OFF_LED		;Tắt led để tạo hiệu ứng led 7 đoạn Bật/Tắt
	MOVLW 0XFF
	MOVWF PORTA
	RLF QUET_LED,1		; Dịch trái QUET_LED
END_NGAT
	BCF INTCON,2       ;Xóa cờ ngắt Timer0
	MOVLW D'131'
	MOVWF TMR0        ;Cài đặt Timer0 tràn 1ms
	;Kết thúc chương trình con phục vụ ngắt
	SWAPF STATUS_TEMP,W
	MOVWF STATUS
	SWAPF W_TEMP,F
	SWAPF W_TEMP, W
	RETFIE
UP_SO       ;Chương trình con thực hiện chức năng tăng số của LED 1..5
	BTFSS MENU,0
	GOTO $+5
	MOVFW LED1
	CALL UP_TEMP
	MOVWF LED1
	RETURN
	BTFSS MENU,1
	GOTO $+5
	MOVFW LED2
	CALL UP_TEMP
	MOVWF LED2
	RETURN
	BTFSS MENU,2
	GOTO $+5
	MOVFW LED3
	CALL UP_TEMP
	MOVWF LED3
	RETURN
	BTFSS MENU,3
	GOTO $+5
	MOVFW LED4
	CALL UP_TEMP
	MOVWF LED4
	RETURN
	BTFSS MENU,4
	GOTO $+4
	MOVFW LED5
	CALL UP_TEMP
	MOVWF LED5
	RETURN
DOWN_SO       ;Chương trình con giảm số của LED 1..5
	BTFSS MENU,0
	GOTO $+5
	MOVFW LED1
	CALL DOWN_TEMP
	MOVWF LED1
	RETURN
	BTFSS MENU,1
	GOTO $+5
	MOVFW LED2
	CALL DOWN_TEMP
	MOVWF LED2
	RETURN
	BTFSS MENU,2
	GOTO $+5
	MOVFW LED3
	CALL DOWN_TEMP
	MOVWF LED3
	RETURN
	BTFSS MENU,3
	GOTO $+5
	MOVFW LED4
	CALL DOWN_TEMP
	MOVWF LED4
	RETURN
	BTFSS MENU,4
	GOTO $+5
	MOVFW LED5
	CALL DOWN_TEMP
	MOVWF LED5
	RETURN
UP_TEMP       ;Chương trình trung gian kiểm tra và tăng giá trị của số
	MOVWF SO
	MOVWF SO_TEMP
	MOVLW D'9'
	SUBWF SO_TEMP,F
	BTFSC STATUS,C
	GOTO $+4
	MOVLW D'1'
	ADDWF SO,F
	GOTO $+3
	MOVLW D'0'
	MOVWF SO
	MOVFW SO
	BCF STATUS,C
	RETURN
DOWN_TEMP       ;Chương trình con kiểm tra và giảm 1 giá trị
	MOVWF SO
	MOVWF SO_TEMP
	MOVLW D'1'
	SUBWF SO_TEMP,F
	BTFSC STATUS,C
	GOTO $+4
	MOVLW D'9'
	MOVWF SO
	GOTO $+3
	MOVLW D'1'
	SUBWF SO,F
	MOVFW SO
	BCF STATUS,C
	RETURN
NHAN_MENU 	;Chương trình con cho chức năng Menu
	BTFSC MENU,5
	GOTO $ + 7
	BTFSC MENU,6
	GOTO $ + 5
	BTFSC MENU,7
	GOTO $ + 3
	RLF MENU,F
	GOTO $ + 3
	MOVLW B'00000001'
	MOVWF MENU
	RETURN
DELAY	;Chương trình con delay 50ms
	MOVLW D'235'
	MOVWF VONG1
	MOVLW D'65'
	MOVWF VONG2
	MOVLW D'1'
	MOVWF VONG3
	DECFSZ VONG1,F
	GOTO $-1
	DECFSZ VONG2,F
	GOTO $-3
	DECFSZ VONG3,F
	GOTO $-5
	RETURN
END
